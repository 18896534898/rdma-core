dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.57)
AC_INIT(libibcm, 1.0-rc1, openib-general@openib.org)
AC_CONFIG_SRCDIR([src/cm.c])
AC_CONFIG_AUX_DIR(config)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(libibcm, 1.0-rc1)

AM_PROG_LIBTOOL

AC_ARG_ENABLE(libcheck, [  --disable-libcheck      do not test for presence of ib libraries],
[       if test x$enableval = xno ; then
                disable_libcheck=yes
        fi
])

dnl Checks for programs
AC_PROG_CC

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_CHECK_SIZEOF(long)

dnl Checks for libraries
if test "$disable_libcheck" != "yes"
then
AC_CHECK_LIB(ibverbs, ibv_get_device_list, [],
    AC_MSG_ERROR([ibv_get_device_list() not found.  libibcm requires libibverbs.]))
#Need librdmacm for cmpost test program.
#AC_CHECK_LIB(rdmacm, rdma_create_id, [],
#    AC_MSG_ERROR([rdma_create_id() not found.  ucmpost requires librdmacm.]))
fi

dnl Checks for header files.
AC_HEADER_STDC
if test "$disable_libcheck" != "yes"
then
AC_CHECK_HEADER(infiniband/verbs.h, [],
    AC_MSG_ERROR([<infiniband/verbs.h> not found.  Is libibverbs installed?]))
AC_CHECK_HEADER(infiniband/marshall.h, [],
    AC_MSG_ERROR([<infiniband/marshall.h> not found.  Is libibverbs installed?]))
fi

AC_CACHE_CHECK(whether ld accepts --version-script, ac_cv_version_script,
    if test -n "`$LD --help < /dev/null 2>/dev/null | grep version-script`"; then
        ac_cv_version_script=yes
    else
        ac_cv_version_script=no
    fi)

AM_CONDITIONAL(HAVE_LD_VERSION_SCRIPT, test "$ac_cv_version_script" = "yes")

AC_CONFIG_FILES([Makefile libibcm.spec])
AC_OUTPUT
